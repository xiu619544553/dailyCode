# <center> `__weak`原理

### 目录
- 一、`__weak` 初探
- 二、分析底层源码实现
    - 1、源码下载
    - 2、方法 `objc_initWeak`
    - 3、方法 `storeWeak`
        - 3.1、方法 `storeWeak`
        - 3.2、结构体 `SideTables`
        - 3.3、结构体 `SideTable`
        - 3.4、结构体 `weak_table_t`
        - 3.5、结构体 `weak_entry_t`
        - 3.6、方法 `weak_register_no_lock`
            - 3.6.1、方法 `weak_entry_for_referent`
            - 3.6.2、方法 `append_referrer`
        - 3.7、方法 `weak_unregister_no_lock`
    - 4、方法 `dealloc`
        - 4.1、 方法 `dealloc` 
        - 4.2、方法 `_objc_rootDealloc`
        - 4.3、方法 `rootDealloc`
        - 4.4、方法 `object_dispose`
        - 4.5、方法 `clearDeallocating`
        - 4.6、方法 `clearDeallocating_slow`
        - 4.7、方法 `weak_clear_no_lock`
- 三、总结


### 一、`__weak` 初探

```
NSObject *o = [[NSObject alloc] init];

__weak id weakPtr = o;  

NSLog(@"o=%@、weakPtr=%@", o, weakPtr);   // obj与p指向同一个对象
NSLog(@"o=%p、weakPtr=%p", &o, &weakPtr); // obj指针变量 与 p是不同的。意味着，对象o，被两个指针变量引用。
```

输出结果：
```shell
2021-11-10 16:34:19.651342+0800 __weak_analysis[37648:301251] o=<NSObject: 0x10300b090>、weakPtr=<NSObject: 0x10300b090>
2021-11-10 16:34:19.652486+0800 __weak_analysis[37648:301251] o=0x7ffeefbff418、weakPtr=0x7ffeefbff410
```

打开 `Xcode` 汇编调试，我们可以发现 `__weak id weakPtr = o;` 底层调用了 `objc_initWeak` 函数。

![汇编调试](https://github.com/xiu619544553/dailyCode/blob/master/icons/_weak_analysis.png)

下面我们来看一下 `objc_initWeak` 方法是如何实现的。

### 二、分析底层源码实现

#### 1、源码下载
下载地址：[链接](https://opensource.apple.com/tarballs/objc4/)

本文使用的源码版本：`objc4-756.2`

#### 2、方法 `objc_initWeak`

通过调试我们知道，执行代码 `__weak id weakPtr = o`，系统底层会调用 `objc_initWeak`方法。

`objc_initWeak` 源码实现如下：

```
/** 
 * 初始化指向某个对象位置的新弱指针 
 * Initialize a fresh weak pointer to some object location. 
 * It would be used for code like: 
 * 
 * (The nil case) 
 * __weak id weakPtr;
 * (The non-nil case) 
 * NSObject *o = ...;
 * __weak id weakPtr = o;
 * 
 * This function IS NOT thread-safe with respect to concurrent 
 * modifications to the weak variable. (Concurrent weak clear is safe.)
 *
 * @param location Address of __weak ptr. 
 * @param newObj Object ptr. 
 */
id objc_initWeak(id *location, id newObj)
{
    if (!newObj) {
        *location = nil;
        return nil;
    }

    return storeWeak<DontHaveOld, DoHaveNew, DoCrashIfDeallocating>
        (location, (objc_object*)newObj);
}
```

该方法有两个参数：`location` 和 `newObj`。
* `location`，弱引用指针（当该指针指向的对象被释放时，该指针会被置为 `nil`）。
* `newObj`，弱引用指针指向的对象，即示例代码中的对象`o`。

`objc_initWeak` 是 `__weak` 底层实现的函数入口。该方法中调用了 `storeWeak`。

#### 3、方法 `storeWeak`

##### 3.1、方法 `storeWeak`
`storeWeak`方法的作用是：
* 如果 `newObj ≠ nil`，表示添加`弱引用指针及其所引用的对象`到弱引用表。
* 如果 `newObj == nil`，表示将 `weak指针`置为nil。

源码如下：
```
// HaveOld 代表是否有旧的引用，如果为true，则代表有旧的引用需要释放，这个值可能是nil。
// HaveNew 代表是否有新的引用，如果为true，则代表要存储新的引用，这个值可能是nil。
// 如果 CrashIfDeallocating 为true，则当newObj正在释放或newObj的类不支持弱引用时，进程将暂停。如果 CrashIfDeallocating 为 false，则存储nil。
//
// Update a weak variable.
// If HaveOld is true, the variable has an existing value 
//   that needs to be cleaned up. This value might be nil.
// If HaveNew is true, there is a new value that needs to be 
//   assigned into the variable. This value might be nil.
// If CrashIfDeallocating is true, the process is halted if newObj is 
//   deallocating or newObj's class does not support weak references. 
//   If CrashIfDeallocating is false, nil is stored instead.
enum CrashIfDeallocating {
    DontCrashIfDeallocating = false,
    DoCrashIfDeallocating = true
};
template <HaveOld haveOld, HaveNew haveNew,
          CrashIfDeallocating crashIfDeallocating>
static id storeWeak(id *location, objc_object *newObj)   // 添加一对(weak pointer、object）到弱引用表里。其中 newObj可以传nil，表示将 weak指针置为nil
{
    assert(haveOld  ||  haveNew);
    if (!haveNew) assert(newObj == nil);

    Class previouslyInitializedClass = nil;
    id oldObj;
    SideTable *oldTable;
    SideTable *newTable;

    // Acquire locks for old and new values. 为旧值和新值获取锁。
    // Order by lock address to prevent lock ordering problems.  按锁地址订购，防止锁顺序问题。
    // Retry if the old value changes underneath us. 如果我们下面的旧值发生了变化，请重试。
 retry:
    if (haveOld) { // 如果weak ptr之前弱引用过一个obj，则将这个obj所对应的SideTable取出，赋值给oldTable
        oldObj = *location;
        oldTable = &SideTables()[oldObj];
    } else { // 如果weak ptr之前没有弱引用过一个obj，则oldTable = nil
        oldTable = nil;
    }
    if (haveNew) { // 如果weak ptr要weak引用一个新的obj，则将该obj对应的SideTable取出，赋值给newTable
        newTable = &SideTables()[newObj];
    } else { // 如果weak ptr不需要引用一个新obj，则newTable = nil
        newTable = nil;
    }

    // 加锁操作，防止多线程中竞争冲突
    SideTable::lockTwo<haveOld, haveNew>(oldTable, newTable);

    // location 应该与 oldObj 保持一致，如果不同，说明当前的 location 已经处理过 oldObj 可是又被其他线程所修改
    if (haveOld  &&  *location != oldObj) {
        SideTable::unlockTwo<haveOld, haveNew>(oldTable, newTable);
        goto retry;
    }

    // 确保弱引用对象都已经初始化isa，可以防止弱引用机制和初始化机制之间的死锁。
    // Prevent a deadlock between the weak reference machinery
    // and the +initialize machinery by ensuring that no 
    // weakly-referenced object has an un-+initialized isa.
    // 如果有新值，判断新值所属的类是否已经初始化，如果没有初始化，则先执行初始化，防止+initialize内部调用storeWeak产生死锁
    if (haveNew  &&  newObj) {
        Class cls = newObj->getIsa();
        if (cls != previouslyInitializedClass  &&  
            !((objc_class *)cls)->isInitialized()) // 如果cls还没有初始化，先初始化，再尝试设置weak
        {
            SideTable::unlockTwo<haveOld, haveNew>(oldTable, newTable);
            class_initialize(cls, (id)newObj);

            // If this class is finished with +initialize then we're good.
            // If this class is still running +initialize on this thread 
            // (i.e. +initialize called storeWeak on an instance of itself)
            // then we may proceed but it will appear initializing and 
            // not yet initialized to the check above.
            // Instead set previouslyInitializedClass to recognize it on retry.
            previouslyInitializedClass = cls;

            // 重新获取一遍newObj，这时的newObj应该已经初始化过了
            goto retry;
        }
    }

    // 如果有旧值，则清除掉。
    // Clean up old value, if any.
    if (haveOld) {
        weak_unregister_no_lock(&oldTable->weak_table, oldObj, location); // 注销已注册的弱引用
    }

    // 如果有新的值，则分配新的值。
    // Assign new value, if any.
    if (haveNew) {
        // (1) 调用weak_register_no_lock方法，将weak ptr的地址记录到newObj对应的weak_entry_t中
        newObj = (objc_object *)
            weak_register_no_lock(&newTable->weak_table, (id)newObj, location, 
                                  crashIfDeallocating);
        // weak_register_no_lock returns nil if weak store should be rejected

        // (2) 更新newObj的isa的weakly_referenced bit标志位
        // Set is-weakly-referenced bit in refcount table.
        if (newObj  &&  !newObj->isTaggedPointer()) {
            newObj->setWeaklyReferenced_nolock();
        }

        // （3）*location 赋值，也就是将weak ptr直接指向了newObj。可以看到，这里并没有将newObj的引用计数+1
        // Do not set *location anywhere else. That would introduce a race.
        *location = (id)newObj;
    }
    else {
        // No new value. The storage is not changed.
    }
    
    // 解锁，其他线程可以访问oldTable, newTable了
    SideTable::unlockTwo<haveOld, haveNew>(oldTable, newTable);

    return (id)newObj;
}
```

storeWeak 方法的实现并不复杂，下面我们来分析下该方法的实现。

* `storeWeak`方法实际上是接收了5个参数，分别是`haveOld`、`haveNew`和`crashIfDeallocating` ，这三个参数都是以模板的方式传入的，是三个bool类型的参数。 分别表示weak指针之前是否指向了一个弱引用，`weak` 指针是否需要指向一个新的引用，若果被弱引用的对象正在析构，此时再弱引用该对象是否应该crash。
* 该方法维护了 `oldTable` 和 `newTable` 分别表示旧的引用弱表和新的弱引用表，它们都是 `SideTable` 的 `hash` 表。
* 如果weak指针之前指向了一个弱引用，则会调用 `weak_unregister_no_lock` 方法将旧的weak指针地址移除。
* 如果weak指针需要指向一个新的引用，则会调用 `weak_register_no_lock` 方法将新的weak指针地址添加到弱引用表中。
* 调用 `setWeaklyReferenced_nolock` 方法修改weak新引用的对象的bit标志位


源码中用到结构体 `SideTables`、`SideTable`等，方法`weak_unregister_no_lock`、`weak_register_no_lock`，我们一一分析。

##### 3.2、 结构体 `SideTables`

`SideTables` 是一个全局的 `hash` 表,用来存储对象多余的引用计数，以及弱引用表。

`SideTables` 类型：`StripedMap<SideTable>`。`SideTables` 的使用：`SideTable *table = &SideTables()[obj]` 它的作用正是根据 `objc_object` 的指针计算出哈希值，然后从 `SideTables` 这张全局哈希表中找到 `obj` 所对应的 `SideTable`。

##### 3.3、结构体 `SideTable`

`SideTable` 定义位于 `NSObject.mm` 文件中。它管理了两块对我们而言超级重要的内容，一块是 `RefcountMap refcnts` 管理对象的引用计数，一块是 `weak_table_t weak_table` 管理对象的弱引用变量。`refcnts` 涉及的内容本篇先不关注，后面学习 `objc_object` 的时候再深入学习。本篇我们着重学习 `weak_table` 涉及的内容。

```
struct SideTable {
    spinlock_t slock; // 每张 SideTable 都自带一自旋锁，用于上锁/解锁 SideTable
    RefcountMap refcnts; // 用来存储OC对象的引用计数的 hash表(仅在未开启isa优化或在isa优化情况下isa_t的引用计数溢出时才会用到)
    // 存储对象弱引用指针的hash表。是OC中weak功能实现的核心数据结构。
    // 将对象id存储为key，将weak_entry_t结构体存储为value。如果对象id有弱引用存在，则可从中找到对象的 weak_entry_t
    weak_table_t weak_table;

    // 构造函数
    SideTable() {
        memset(&weak_table, 0, sizeof(weak_table));
    }

    // 析构函数
    ~SideTable() {
        _objc_fatal("Do not delete SideTable.");
    }

    void lock() { slock.lock(); }
    void unlock() { slock.unlock(); }
    void forceReset() { slock.forceReset(); }

    // Address-ordered lock discipline for a pair of side tables.
    
    // HaveOld 和 HaveNew 分别表示 lock1 和 lock2 是否存在，
    // 表示 __weak 变量是否指向有旧值和目前要指向的新值。
    
    // lock1 代表旧值对象所处的 SideTable
    // lock2 代表新值对象所处的 SideTable
    
    // lockTwo 是根据谁有值就调谁的锁，触发加锁 (C++ 方法重载)，
    // 如果两个都有值，那么两个都加锁，并且根据谁低，先给谁加锁，然后另一个后加锁
    template<HaveOld, HaveNew>
    static void lockTwo(SideTable *lock1, SideTable *lock2);
    
    // 同上，对 slock 解锁
    template<HaveOld, HaveNew>
    static void unlockTwo(SideTable *lock1, SideTable *lock2);
};
```

 `SideTable` 结构很清晰，3 个成员变量:
* `spinlock_t slock`: 自旋锁，保证操作 `SideTable` 时的线程安全。看前面的两大块 `weak_table_t` 和 `weak_entry_t` 的时候，看到它们所有的操作函数都没有提及加解锁的事情，如果你仔细观察的话会发现它们的函数名后面都有一个 `no_lock` 的小尾巴，正是用来提醒我们，它们的操作完全并没有涉及加锁。其实它们是把保证它们线程安全的任务交给了 `SideTable`，下面可以看到 `SideTable` 提供的函数都是线程安全的，而这都是由 slock 来完成的。
* `RefcountMap refcnts`: 以 `DisguisedPtr<objc_object>` 为 `key`，以 `size_t` 为 `value` 的哈希表，用来存储对象的引用计数（仅在未使用 `isa` 优化或者 `isa` 优化情况下 isa_t 中保存的引用计数溢出时才会用到，这里涉及到 `isa_t` 里的 `uintptr_t has_sidetable_rc` 和 `uintptr_t extra_rc` 两个字段，以前只是单纯的看 `isa` 的结构，到这里终于被用到了，还有这时候终于知道 `rc` 其实是 `refcount(引用计数)` 的缩写）。作为哈希表，它使用的是平方探测法从哈希表中取值，而 `weak_table_t` 则是线性探测（开放寻址法）。（`RefcountMap` 留在引用计数相关文章中来详细分析。）
* `weak_table_t weak_table` 存储对象弱引用的哈希表，是 `weak` 功能实现的核心数据结构。将对象id存储为key，将`weak_entry_t`结构体存储为value。如果对象id有弱引用存在，则可从中找到对象的 `weak_entry_t`。


##### 3.4、结构体 `weak_table_t`

```
/**
 * 弱引用表。将对象id存储为key，将weak_entry_t结构体存储为它们的value。
 * The global weak references table. Stores object ids as keys,
 * and weak_entry_t structs as their values.
 */
struct weak_table_t {
    weak_entry_t *weak_entries; // hash数组，用来存储弱引用对象的相关信息weak_entry_t
    size_t    num_entries;  // hash数组中的元素个数
    uintptr_t mask; // hash数组长度-1，会参与hash计算。（注意，这里是hash数组的长度，而不是元素个数。比如，数组长度可能是64，而元素个数仅存了2个）
    uintptr_t max_hash_displacement; // 可能会发生的hash冲突的最大次数，用于判断是否出现了逻辑错误（hash表中的冲突次数绝不会超过改值）
};
```

##### 3.5、结构体 `weak_entry_t`

```
/**
 * The internal structure stored in the weak references table. 
 * It maintains and stores
 * a hash set of weak references pointing to an object.
 * If out_of_line_ness != REFERRERS_OUT_OF_LINE then the set
 * is instead a small inline array.
 */
#define WEAK_INLINE_COUNT 4

// out_of_line_ness field overlaps with the low two bits of inline_referrers[1].
// inline_referrers[1] is a DisguisedPtr of a pointer-aligned address.
// The low two bits of a pointer-aligned DisguisedPtr will always be 0b00
// (disguised nil or 0x80..00) or 0b11 (any other address).
// Therefore out_of_line_ness == 0b10 is used to mark the out-of-line state.
#define REFERRERS_OUT_OF_LINE 2

struct weak_entry_t {
    DisguisedPtr<objc_object> referent;
    
    // 引用该对象的对象列表，联合。引用个数小于4，用inline_referrers数组。 用个数大于4，用动态数组weak_referrer_t *referrers
    union {
        struct {
            weak_referrer_t *referrers;              // 弱引用该对象的对象指针地址的hash数组
            uintptr_t        out_of_line_ness : 2;   // 是否使用动态hash数组标记位
            uintptr_t        num_refs : PTR_MINUS_2; // hash数组中的元素个数
            uintptr_t        mask;                   // hash数组长度-1，会参与hash计算。（注意，这里是hash数组的长度，而不是元素个数。比如，数组长度可能是64，而元素个数仅存了2个）素个数）。
            uintptr_t        max_hash_displacement;  // 可能会发生的hash冲突的最大次数，用于判断是否出现了逻辑错误（hash表中的冲突次数绝不会超过改值）
        };
        struct {
            // out_of_line_ness field is low bits of inline_referrers[1]
            weak_referrer_t  inline_referrers[WEAK_INLINE_COUNT];
        };
    };

    bool out_of_line() {
        return (out_of_line_ness == REFERRERS_OUT_OF_LINE);
    }

    weak_entry_t& operator=(const weak_entry_t& other) {
        memcpy(this, &other, sizeof(other));
        return *this;
    }

    // 构造方法，里面初始化了静态数组
    weak_entry_t(objc_object *newReferent, objc_object **newReferrer)
        : referent(newReferent)
    {
        inline_referrers[0] = newReferrer;
        for (int i = 1; i < WEAK_INLINE_COUNT; i++) {
            inline_referrers[i] = nil;
        }
    }
};
```

##### 3.6、方法 `weak_register_no_lock`

作用：注册一个新的(对象、弱指针)对。如果不存在，则创建一个新的弱对象项。

源码：
```
/** 
 * Registers a new (object, weak pointer) pair. Creates a new weak 注册一个新的(对象、弱指针)对。如果不存在，则创建一个新的弱对象项。
 * object entry if it does not exist.
 * 
 * @param weak_table The global weak table.（弱引用表）
 * @param referent_id The object pointed to by the weak reference.（弱引用所指向的对象）
 * @param referrer_id The weak pointer address.（weak弱引用指针）
 */
id 
weak_register_no_lock(weak_table_t *weak_table, id referent_id, 
                      id *referrer_id, bool crashIfDeallocating)
{
    // 弱引用所指向的对象
    objc_object *referent = (objc_object *)referent_id;
    objc_object **referrer = (objc_object **)referrer_id;

    if (!referent  ||  referent->isTaggedPointer()) return referent_id;

    // ensure that the referenced object is viable（确保引用的对象是可行的）
    bool deallocating;
    if (!referent->ISA()->hasCustomRR()) {
        deallocating = referent->rootIsDeallocating();
    }
    else {
        BOOL (*allowsWeakReference)(objc_object *, SEL) = 
            (BOOL(*)(objc_object *, SEL))
            object_getMethodImplementation((id)referent, 
                                           SEL_allowsWeakReference);
        if ((IMP)allowsWeakReference == _objc_msgForward) {
            return nil;
        }
        deallocating =
            ! (*allowsWeakReference)(referent, SEL_allowsWeakReference);
    }

    if (deallocating) {
        if (crashIfDeallocating) {
            _objc_fatal("Cannot form weak reference to instance (%p) of "
                        "class %s. It is possible that this object was "
                        "over-released, or is in the process of deallocation.",
                        (void*)referent, object_getClassName((id)referent));
        } else {
            return nil;
        }
    }

    // now remember it and where it is being stored （现在记住它和它被储存的地方）
    weak_entry_t *entry;
    if ((entry = weak_entry_for_referent(weak_table, referent))) { // 如果这个 weak 变量不是第一个的话，表明这个对象此时已经存在于 weak_table_t 哈希表中了，此时只需要把这个指向它的 weak 变量的地址保存进该对象的 weak_entry_t 哈希表中
        append_referrer(entry, referrer); // 把 __weak指针保存进对象的 weak_entry_t 哈希表中。
    } 
    else {
        weak_entry_t new_entry(referent, referrer);
        weak_grow_maybe(weak_table);
        weak_entry_insert(weak_table, &new_entry); //  弱引用所指向的对象注册进 weak_table_t 哈希表中。在对象的弱引用表中添加新的 weak_entry_t。
    }

    // Do not set *referrer. objc_storeWeak() requires that the 
    // value not change.

    return referent_id;
}
```

###### 3.6.1、方法 `weak_entry_for_referent`

作用：根据给定的弱引用引用的对象，返回它所在的 `weak_entry_t`，如果没有则返回NULL

```
/** 
 * Return the weak reference table entry for the given referent. 根据给定的弱引用引用的对象，返回它所在的 weak_entry_t，如果没有则返回NULL
 * If there is no entry for referent, return NULL. 
 * Performs a lookup.
 *
 * @param weak_table 弱引用表
 * @param referent The object. Must not be nil. 弱引用所指向的对象
 * 
 * @return The table of weak referrers to this object. 
 */
static weak_entry_t *
weak_entry_for_referent(weak_table_t *weak_table, objc_object *referent)
{
    assert(referent);

    // weak_table_t 中哈希数组的入口
    weak_entry_t *weak_entries = weak_table->weak_entries;

    if (!weak_entries) return nil;

    // hash_pointer 哈希函数返回值与 mask 做与操作，防止 index 越界，这里的 & mask 操作很巧妙，后面会进行详细讲解。
    size_t begin = hash_pointer(referent) & weak_table->mask;
    size_t index = begin;
    size_t hash_displacement = 0;
    
    // 如果未发生哈希冲突的话，这里 weak_table->weak_entries[index] 就是要找的 weak_entry_t 了
    while (weak_table->weak_entries[index].referent != referent) {
        
        // 如果发生了哈希冲突，+1 继续往下探测（开放寻址法）
        index = (index+1) & weak_table->mask;
        
        // 如果 index 每次加 1 加到值等于 begin 还没有找到 weak_entry_t，则触发 bad_weak_table 致命错误
        if (index == begin) bad_weak_table(weak_table->weak_entries);
        
        // 记录探测偏移了多远
        hash_displacement++;
        
        // 如果探测偏移超过了 weak_table_t 的 max_hash_displacement，说明在 weak_table 中没有 referent 的 weak_entry_t，则直接返回 nil
        if (hash_displacement > weak_table->max_hash_displacement) {
            return nil;
        }
    }
    
    // 到这里遍找到了 weak_entry_t，然后取它的地址并返回。
    return &weak_table->weak_entries[index];
}
```

###### 3.6.2、方法 `append_referrer`

```
/** 
 * Add the given referrer to set of weak pointers in this entry.  将给定的引用添加到此条目中的弱指针集。
 * Does not perform duplicate checking (b/c weak pointers are never
 * added to a set twice). 
 *
 * @param entry The entry holding the set of weak pointers. 保存弱指针集合的项。
 * @param new_referrer The new weak pointer to be added.    要添加的新弱指针。
 */
static void append_referrer(weak_entry_t *entry, objc_object **new_referrer)
{
    if (! entry->out_of_line()) { // 如果weak_entry 尚未使用动态数组，走这里
        // Try to insert inline.
        for (size_t i = 0; i < WEAK_INLINE_COUNT; i++) {
            if (entry->inline_referrers[i] == nil) {
                entry->inline_referrers[i] = new_referrer;
                return;
            }
        }

        // 如果inline_referrers的位置已经存满了，则要转型为referrers，做动态数组。
        // Couldn't insert inline. Allocate out of line.
        weak_referrer_t *new_referrers = (weak_referrer_t *)
            calloc(WEAK_INLINE_COUNT, sizeof(weak_referrer_t));
        // This constructed table is invalid, but grow_refs_and_insert
        // will fix it and rehash it.
        for (size_t i = 0; i < WEAK_INLINE_COUNT; i++) {
            new_referrers[i] = entry->inline_referrers[i];
        }
        entry->referrers = new_referrers;
        entry->num_refs = WEAK_INLINE_COUNT;
        entry->out_of_line_ness = REFERRERS_OUT_OF_LINE;
        entry->mask = WEAK_INLINE_COUNT-1;
        entry->max_hash_displacement = 0;
    }

    // 对于动态数组的附加处理：
    // 断言： 此时一定使用的动态数组
    assert(entry->out_of_line());

    
    if (entry->num_refs >= TABLE_SIZE(entry) * 3/4) { // 如果动态数组中元素个数大于或等于数组位置总空间的3/4，则扩展数组空间为当前长度的一倍
        return grow_refs_and_insert(entry, new_referrer); // 扩容，并插入
    }
    
    // 如果不需要扩容，直接插入到weak_entry中
    // 注意，weak_entry是一个哈希表，key：w_hash_pointer(new_referrer) value: new_referrer

    size_t begin = w_hash_pointer(new_referrer) & (entry->mask); // '& (entry->mask)' 确保了 begin的位置只能大于或等于 数组的长度
    size_t index = begin; // 初始的hash index
    size_t hash_displacement = 0; // 用于记录hash冲突的次数，也就是hash再位移的次数
    while (entry->referrers[index] != nil) {
        hash_displacement++;
        index = (index+1) & entry->mask;  // index + 1, 移到下一个位置，再试一次能否插入。（这里要考虑到entry->mask取值，一定是：0x111, 0x1111, 0x11111, ... ，因为数组每次都是*2增长，即8， 16， 32，对应动态数组空间长度-1的mask，也就是前面的取值。）
        if (index == begin) bad_weak_table(entry); // index == begin 意味着数组绕了一圈都没有找到合适位置，这时候一定是出了什么问题。
    }
    if (hash_displacement > entry->max_hash_displacement) { // 记录最大的hash冲突次数, max_hash_displacement意味着: 我们尝试至多max_hash_displacement次，肯定能够找到object对应的hash位置
        entry->max_hash_displacement = hash_displacement;
    }
    // 将ref存入hash数组，同时，更新元素个数num_refs
    weak_referrer_t &ref = entry->referrers[index];
    ref = new_referrer;
    entry->num_refs++;
}
```
这段代码首先确定是使用定长数组还是动态数组，如果是使用定长数组，则直接将weak指针地址添加到数组即可，如果定长数组已经用尽，则需要将定长数组中的元素转存到动态数组中。


##### 3.7、方法 `weak_unregister_no_lock`

作用：注销已注册的弱引用。 从弱引用表里移除一对（object, weak pointer）。（从对象的 weak_entry_t 哈希表中移除一个 weak 变量的地址）

```
/** 
 * Unregister an already-registered weak reference. 注销已注册的弱引用。
 * This is used when referrer's storage is about to go away, but referent
 * isn't dead yet. (Otherwise, zeroing referrer later would be a
 * bad memory access.)
 * Does nothing if referent/referrer is not a currently active weak reference.
 * Does not zero referrer.
 * 
 * FIXME currently requires old referent value to be passed in (lame)
 * FIXME unregistration should be automatic if referrer is collected
 * 
 * @param weak_table The global weak table.
 * @param referent The object. 弱引用指向的对象
 * @param referrer The weak reference. __weak指针
 */
void
weak_unregister_no_lock(weak_table_t *weak_table, id referent_id, id *referrer_id)
{
    // weak_unregister_no_lock(&oldTable->weak_table, oldObj, location); // 注销已注册的弱引用
    
    objc_object *referent = (objc_object *)referent_id;   // 弱引用引用的对象
    objc_object **referrer = (objc_object **)referrer_id; // __weak指向

    weak_entry_t *entry;

    if (!referent) return;

    if ((entry = weak_entry_for_referent(weak_table, referent))) {
        remove_referrer(entry, referrer);
        bool empty = true;
        if (entry->out_of_line()  &&  entry->num_refs != 0) {
            empty = false;
        }
        else {
            for (size_t i = 0; i < WEAK_INLINE_COUNT; i++) {
                if (entry->inline_referrers[i]) {
                    empty = false; 
                    break;
                }
            }
        }

        if (empty) {
            weak_entry_remove(weak_table, entry);
        }
    }

    // Do not set *referrer = nil. objc_storeWeak() requires that the 
    // value not change.
}
```

到这里为止就是对于一个对象做weak引用时底层做的事情，用weak引用对象后引用计数并不会加1，当对象释放时，所有weak引用它的指针又是如何自动设置为nil的呢？

#### 4、方法 `dealloc`

当对象的引用计数为0时，底层会调用 `_objc_rootDealloc` 方法对对象进行释放，而在`_objc_rootDealloc` 方法里面会调用 `rootDealloc` 方法。如下是 `rootDealloc` 方法的代码实现。

##### 4.1、 方法 `dealloc` 

源码在 `NSObject.mm`中：
```
// Replaced by NSZombies
- (void)dealloc {
    _objc_rootDealloc(self);
}
```

##### 4.2、方法 `_objc_rootDealloc`

```
void _objc_rootDealloc(id obj)
{
    assert(obj);

    obj->rootDealloc();
}
```

##### 4.3、方法 `rootDealloc`

```
inline void objc_object::rootDealloc()
{
    if (isTaggedPointer()) return; // Tagged Pointer指针的值不再是地址了，而是真正的值。所以，实际上它不再是一个对象了，它只是一个披着对象皮的普通变量而已。所以，它的内存并不存储在堆中，也不需要malloc和free，所以直接 return

    if (fastpath(isa.nonpointer  &&  
                 !isa.weakly_referenced  &&  
                 !isa.has_assoc  &&  
                 !isa.has_cxx_dtor  &&  
                 !isa.has_sidetable_rc))
    {
        assert(!sidetable_present());
        free(this);
    } 
    else {
        object_dispose((id)this);
    }
}
```

* 首先判断对象是否是 `Tagged Pointer`，如果是则直接返回。
* 如果对象是采用了优化的 isa` 计数方式，且同时满足对象没有被weak引用!`isa.weakly_referenced`、没有关联对象 `!isa.has_assoc` 、没有自定义的C++析构方法 `!isa.has_cxx_dtor`、没有用到 `SideTable` 来引用计数 `!isa.has_sidetable_rc`则直接快速释放。
* 如果不能满足2中的条件，则会调用 `object_dispose` 方法。

##### 4.4、方法 `object_dispose`

```
/***********************************************************************
* object_dispose
* fixme
* Locking: none
**********************************************************************/
id 
object_dispose(id obj)
{
    if (!obj) return nil;

    objc_destructInstance(obj);    
    free(obj);

    return nil;
}


/***********************************************************************
* objc_destructInstance
* Destroys an instance without freeing memory. 
* Calls C++ destructors.
* Calls ARC ivar cleanup.
* Removes associative references.
* Returns `obj`. Does nothing if `obj` is nil.
**********************************************************************/
void *objc_destructInstance(id obj) 
{
    if (obj) {
        // Read all of the flags at once for performance.
        bool cxx = obj->hasCxxDtor();
        bool assoc = obj->hasAssociatedObjects();

        // This order is important.
        if (cxx) object_cxxDestruct(obj);
        if (assoc) _object_remove_assocations(obj);
        obj->clearDeallocating();
    }

    return obj;
}
```

上面这一段代码很清晰，如果有自定义的C++析构方法，则调用C++析构函数。如果有关联对象，则移除关联对象并将其自身从`Association Manager`的map中移除。调用`clearDeallocating` 方法清除对象的相关引用。

##### 4.5、方法 `clearDeallocating`

```
inline void 
objc_object::clearDeallocating()
{
    if (slowpath(!isa.nonpointer)) {
        // Slow path for raw pointer isa.
        sidetable_clearDeallocating();
    }
    else if (slowpath(isa.weakly_referenced  ||  isa.has_sidetable_rc)) {
        // Slow path for non-pointer isa with weak refs and/or side table data.
        clearDeallocating_slow();
    }

    assert(!sidetable_present());
}
```

`clearDeallocating` 中有两个分支，先判断对象是否采用了优化`isa`引用计数，如果没有的话则需要清理对象存储在 `SideTable` 中的引用计数数据。如果对象采用了优化isa引用计数，则判断是否有使用SideTable的辅助引用计数 `(isa.has_sidetable_rc)` 或者有 `weak`引用`(isa.weakly_referenced)`，符合这两种情况中一种的，调用 `clearDeallocating_slow` 方法。

##### 4.6、方法 `clearDeallocating_slow`

```
// Slow path of clearDeallocating() 
// for objects with nonpointer isa
// that were ever weakly referenced 
// or whose retain count ever overflowed to the side table.
NEVER_INLINE void
objc_object::clearDeallocating_slow()
{
    assert(isa.nonpointer  &&  (isa.weakly_referenced || isa.has_sidetable_rc));

    SideTable& table = SideTables()[this];
    table.lock();
    if (isa.weakly_referenced) {
        weak_clear_no_lock(&table.weak_table, (id)this);
    }
    if (isa.has_sidetable_rc) {
        table.refcnts.erase(this);
    }
    table.unlock();
}
```

在这里我们关心的是 `weak_clear_no_lock`  方法。这里调用了 `weak_clear_no_lock` 来做 `weak_table` 的清理工作。

##### 4.7、方法 `weak_clear_no_lock`

```
void 
weak_clear_no_lock(weak_table_t *weak_table, id referent_id) 
{
    objc_object *referent = (objc_object *)referent_id;

    weak_entry_t *entry = weak_entry_for_referent(weak_table, referent); // 找到referent在weak_table中对应的weak_entry_t
    if (entry == nil) {
        /// XXX shouldn't happen, but does with mismatched CF/objc
        //printf("XXX no entry for clear deallocating %p\n", referent);
        return;
    }

    // zero out references
    weak_referrer_t *referrers;
    size_t count;
    
    // 找出weak引用referent的weak 指针地址数组以及数组长度
    if (entry->out_of_line()) {
        referrers = entry->referrers;
        count = TABLE_SIZE(entry);
    } 
    else {
        referrers = entry->inline_referrers;
        count = WEAK_INLINE_COUNT;
    }
    
    for (size_t i = 0; i < count; ++i) {
        objc_object **referrer = referrers[i]; // 取出每个weak ptr的地址
        if (referrer) {
            if (*referrer == referent) { // 如果weak ptr确实weak引用了referent，则将weak ptr设置为nil，这也就是为什么weak 指针会自动设置为nil的原因
                *referrer = nil;
            }
            else if (*referrer) { // 如果所存储的weak ptr没有weak 引用referent，这可能是由于runtime代码的逻辑错误引起的，报错
                _objc_inform("__weak variable at %p holds %p instead of %p. "
                             "This is probably incorrect use of "
                             "objc_storeWeak() and objc_loadWeak(). "
                             "Break on objc_weak_error to debug.\n", 
                             referrer, (void*)*referrer, (void*)referent);
                objc_weak_error();
            }
        }
    }
    
    weak_entry_remove(weak_table, entry); // 由于referent要被释放了，因此referent的weak_entry_t也要移除出weak_table
}
```

### 三、总结

1. `weak` 的原理在于底层维护了一张 `weak_table_t` 结构的 `hash表`，`key`是所指对象的地址，`value` 是 `weak`指针的地址数组。
2. `weak` 关键字的作用是弱引用，所引用对象的计数器不会加1，并在引用对象被释放的时候自动被设置为 `nil`。
3. 对象释放时，调用 `clearDeallocating` 函数根据对象地址获取所有weak指针地址的数组，然后遍历这个数组把其中的数据设为 `nil`，最后把这个entry从weak表中删除，最后清理对象的记录。
4. 文章中介绍了 `SideTable`、`weak_table_t`、`weak_entry_t`这样三个结构，它们之间的关系如下图所示。


![weak各个数据关系的关系](https://github.com/xiu619544553/dailyCode/blob/master/icons/_weak_relation.png)




参考资料
* [iOS weak 底层实现原理(一)：SideTable|s、weak_table_t、weak_entry_t 等数据结构
](https://juejin.cn/post/6865468675940417550#heading-0)
* [iOS底层原理：weak的实现原理](https://juejin.cn/post/6844904101839372295#heading-1)
* [iOS weak 的实现](https://www.jianshu.com/p/3f740d2085e7)